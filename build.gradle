/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2018-2019 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'groovy'
    id 'idea'
    id 'org.kordamp.gradle.kordamp-parentpom' version '1.8.0'
    id 'org.kordamp.gradle.bintray'           version '0.29.0'
    id 'org.kordamp.gradle.plugin'            version '0.29.0'
}

config {
    info {
        name          = rootProject.name
        description   = pluginDescription
        inceptionYear = '2018'
        tags          = ['jdeprscan', 'jdktool', 'jigsaw', 'modules']

        specification { enabled = false }
    }
    groovydoc {
        replaceJavadoc = true
    }

    plugin {
        enabled             = true
        id                  = project.pluginId
        implementationClass = project.pluginImplementationClass
    }

    bintray {
        skipMavenSync = true
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation "org.zeroturnaround:zt-exec:$ztexecVersion"
}

compileGroovy {
    sourceCompatibility = project.sourceCompatibility
    targetCompatibility = project.targetCompatibility
}

task publishRelease(dependsOn: [bintrayUpload, publishPlugins]) {}
